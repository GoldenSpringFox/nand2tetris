// This file is part of nand2tetris, as taught in The Hebrew University, and
// was written by Aviv Yaish. It is an extension to the specifications given
// [here](https://www.nand2tetris.org) (Shimon Schocken and Noam Nisan, 2017),
// as allowed by the Creative Common Attribution-NonCommercial-ShareAlike 3.0
// Unported [License](https://creativecommons.org/licenses/by-nc-sa/3.0/).

// This chip is an extension of the regular CPU that uses the extended ALU.
// If instruction[15]==0 or (instruction[14]==1 and instruction[13]==1),
// then CpuMul behaves exactly the same as the regular CPU.
// If instruction[15]==1 and instruction[14]==0 the chip will behave as follows:
// | Instruction           | 15 | 14 | 13 | a | c1 | c2 | c3 | c4 | c5 | c6 |
// |-----------------------|:--:|:--:|:--:|:-:|:--:|:--:|:--:|:--:|:--:|:--:|
// | Regular a-instruction |  0 |  * |  * | * |  * |  * |  * |  * |  * |  * |
// | Regular c-instruction |  1 |  1 |  1 | * |  * |  * |  * |  * |  * |  * |
// | dest=A<<;jump         |  1 |  0 |  1 | 0 |  1 |  0 |  0 |  0 |  0 |  0 |
// | dest=D<<;jump         |  1 |  0 |  1 | 0 |  1 |  1 |  0 |  0 |  0 |  0 |
// | dest=M<<;jump         |  1 |  0 |  1 | 1 |  1 |  0 |  0 |  0 |  0 |  0 |
// | dest=A>>;jump         |  1 |  0 |  1 | 0 |  0 |  0 |  0 |  0 |  0 |  0 |
// | dest=D>>;jump         |  1 |  0 |  1 | 0 |  0 |  1 |  0 |  0 |  0 |  0 |
// | dest=M>>;jump         |  1 |  0 |  1 | 1 |  0 |  0 |  0 |  0 |  0 |  0 |
// Where:
// - "<<" is a left shift, and ">>" is a right shift, as defined in project 2.
//   These notations were chosen because they are used in real programming
//   languages.
// - dest and jump can take the same values as in the regular CPU.

CHIP CpuMul {
    IN  
        inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset=1) or continue executing
                         // the current program (reset=0).
    OUT 
        outM[16],        // M value output
        writeM,          // Write into M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction
    
    PARTS:
    And(a=instruction[15], b=instruction[12], out=AMSel);
    Mux16(a=AOut, b=inM, sel=AMSel, out=AMOut);
    ExtendALU(x=DOut, y=AMOut, instruction=instruction[6..14], out=aluOut, out=outM, zr=aluZr, ng=aluNg);
    
    And(a=instruction[15], b=instruction[3], out=writeM);

    Mux16(a=instruction, b=aluOut, sel=instruction[15], out=aIn);
    Not(in=instruction[15], out=AInstruction);
    And(a=instruction[15], b=instruction[5], out=CInstructionLoadA);
    Or(a=AInstruction, b=CInstructionLoadA, out=LoadA);
    ARegister(in=aIn, load=LoadA, out=AOut, out[0..14]=addressM);
    
    And(a=instruction[15], b=instruction[4], out=CInstructionLoadD);
    DRegister(in=aluOut, load=CInstructionLoadD, out=DOut);
    
    Or(a=aluNg, b=aluZr, out=aluNgZr);
    Not(in=aluNgZr, out=aluPs);
    And(a=aluNg, b=instruction[2], out=jumpNg);
    And(a=aluZr, b=instruction[1], out=jumpZr);
    And(a=aluPs, b=instruction[0], out=jumpPs);
    Or(a=jumpNg, b=jumpZr, out=jumpNgZr);
    Or(a=jumpNgZr, b=jumpPs, out=jumpNgZrPs);
    Mux(a=false, b=jumpNgZrPs, sel=instruction[15], out=pcLoad);
    PC(in=AOut, load=pcLoad, inc=true, reset=reset, out[0..14]=pc);
}
